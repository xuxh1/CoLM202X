#include <define.h>

SUBROUTINE aggregation_NDEP (gridndep, dir_rawdata, dir_model_landdata)
   ! ----------------------------------------------------------------------
   ! !DESCRIPTION:
   ! Aggregate the atmospheric nitrogen deposition data from CLM5.
   !
   ! !REFERENCE:
   ! Galloway, J.N., et al. 2004. Nitrogen cycles: past, present, and future. Biogeochem. 70:153-226.
   !
   ! !ORIGINAL:
   ! Xingjie Lu and Shupeng Zhang, 2022
   ! ----------------------------------------------------------------------
   USE precision
   USE MOD_Vars_Global
   USE mod_namelist
   USE spmd_task
   USE mod_grid
   USE mod_landpatch
   USE ncio_block
   USE ncio_vector
#ifdef CoLMDEBUG
   USE mod_colm_debug
#endif

   USE mod_aggregation

   USE MOD_Vars_LCConst

#ifdef SrfdataDiag
   USE mod_srfdata_diag
#endif

   IMPLICIT NONE

   ! arguments:

   TYPE(grid_type),  intent(in) :: gridndep
   CHARACTER(LEN=*), intent(in) :: dir_rawdata
   CHARACTER(LEN=*), intent(in) :: dir_model_landdata

   ! local variables:
   ! ----------------------------------------------------------------------
   CHARACTER(len=256) :: landdir, lndname

   TYPE (block_data_real8_2d) :: NDEP          ! nitrogen deposition (gN/m2/s)
   REAL(r8), allocatable :: NDEP_patches(:), ndep_one(:), area_one(:)
   INTEGER :: itime, ipatch
   CHARACTER(LEN=4) :: cyear
   integer :: start_year, end_year, YY

#ifdef SrfdataDiag
   INTEGER :: typpatch(N_land_classification+1), ityp
   CHARACTER(len=256) :: varname
#endif

   landdir = trim(dir_model_landdata) // '/NDEP/'

#ifdef USEMPI
   CALL mpi_barrier (p_comm_glb, p_err)
#endif
   IF (p_is_master) THEN
      write(*,'(/, A)') 'Aggregate NDEP ...'
      CALL system('mkdir -p ' // trim(adjustl(landdir)))
   ENDIF
#ifdef USEMPI
   CALL mpi_barrier (p_comm_glb, p_err)
#endif

!ifdef SinglePoint
!   IF (USE_SITE_NDEP) THEN
!     RETURN
!  ENDIF
!#endif

   ! ................................................
   ! ... global plant leaf area index
   ! ................................................

   start_year = DEF_simulation_time%start_year
   end_year   = DEF_simulation_time%end_year

   ! ----- NDEP -----
   IF (p_is_io) THEN
      CALL allocate_block_data (gridndep, NDEP)
   ENDIF

   IF (p_is_worker) THEN
      allocate (NDEP_patches (numpatch))
   ENDIF

   DO YY = start_year, end_year

      write(cyear,'(i4.4)') YY
      itime = max(min(YY,2006),1849) - 1848
         !write(c1,'(i4.4)') YY
         ! ---------------------------
         ! read in nitrofen deposition
         ! ---------------------------
      IF (p_is_master) THEN
         write(*,'(A,I4,A9,I4.4,A1,I3)') 'Aggregate NDEP:', YY,'(data in:',itime+1848,')'
      endif

      IF (p_is_io) THEN
         lndname = trim(dir_rawdata)//'/ndep/fndep_colm_hist_simyr1849-2006_1.9x2.5_c100428.nc'
         CALL ncio_read_block_time (lndname, 'NDEP_year', gridndep, itime, NDEP)
      ENDIF

#ifdef USEMPI
         CALL aggregation_data_daemon (gridndep, data_r8_2d_in1 = NDEP)
#endif

         ! ---------------------------------------------------------------
         ! aggregate the plant leaf area index from the resolution of raw data to modelling resolution
         ! ---------------------------------------------------------------

      IF (p_is_worker) THEN
         DO ipatch = 1, numpatch
            CALL aggregation_request_data (landpatch, ipatch, gridndep, area = area_one, &
               data_r8_2d_in1 = NDEP, data_r8_2d_out1 = ndep_one)
            NDEP_patches(ipatch) = sum(ndep_one * area_one) / sum(area_one)
         ENDDO

#ifdef USEMPI
         CALL aggregation_worker_done ()
#endif
      ENDIF

#ifdef USEMPI
      CALL mpi_barrier (p_comm_glb, p_err)
#endif

#ifdef CoLMDEBUG
      CALL check_vector_data ('NDEP value ', NDEP_patches)
#endif

         ! ---------------------------------------------------
         ! write out the plant leaf area index of grid patches
         ! ---------------------------------------------------
      lndname = trim(landdir) // '/NDEP_'//trim(cyear)//'_patches.nc'

      CALL ncio_create_file_vector (lndname, landpatch)
      CALL ncio_define_dimension_vector (lndname, landpatch, 'patch')
      CALL ncio_write_vector (lndname, 'NDEP_patches', 'patch', landpatch, NDEP_patches, 1)

#ifdef SrfdataDiag
      typpatch = (/(ityp, ityp = 0, N_land_classification)/)
      lndname  = trim(dir_model_landdata) // '/diag/NDEP_patch.nc'
      varname = 'NDEP_' // trim(cyear)
      CALL srfdata_map_and_write (NDEP_patches, landpatch%settyp, typpatch, m_patch2diag, &
         -1.0e36_r8, lndname, trim(varname), compress = 1, write_mode = 'one')
#endif
   ENDDO


   IF (p_is_worker) THEN
      IF (allocated(NDEP_patches)) deallocate(NDEP_patches)
      IF (allocated(ndep_one    )) deallocate(ndep_one    )
      IF (allocated(area_one    )) deallocate(area_one    )
   ENDIF

END SUBROUTINE aggregation_NDEP
