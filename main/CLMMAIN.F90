#include <define.h>

SUBROUTINE CLMMAIN ( &
   
         ! 模型运行信息
           ipatch,       idate,        coszen,       deltim,        &
           patchlonr,    patchlatr,    patchclass,   patchtype,     &
           doalb,        dolai,        dosst,        oro,           &


         ! soil information and lake depth
           soil_s_v_alb, soil_d_v_alb, soil_s_n_alb, soil_d_n_alb,  &
           vf_quartz,    vf_gravels,   vf_om,        vf_sand,       &
           wf_gravels,   wf_sand,      porsl,        psi0,          & 
           bsw,                                                     &
#ifdef vanGenuchten_Mualem_SOIL_MODEL
           theta_r,      alpha_vgm,    n_vgm,        L_vgm,         &
           sc_vgm,       fc_vgm,                                    &
#endif
           hksati,       csol,         k_solids,     dksatu,        &
           dksatf,       dkdry,                                     &
#ifdef THERMAL_CONDUCTIVITY_SCHEME_4
           BA_alpha,     BA_beta,                                   &
#endif
           rootfr,       lakedepth,    dz_lake,                     &
#if(defined CaMa_Flood)
         flddepth,    fldfrc,     fevpg_fld, qinfl_fld,             & !flddepth is in mm
#endif

         ! vegetation information
           htop,         hbot,         sqrtdi,                      &
           effcon,       vmax25,                                    &
#ifdef PLANT_HYDRAULIC_STRESS
           kmax_sun,     kmax_sha,     kmax_xyl,     kmax_root,     &
           psi50_sun,    psi50_sha,    psi50_xyl,    psi50_root,    &
           ck,                                                      &
#endif
           slti,         hlti,                                      &
           shti,         hhti,         trda,         trdm,          &
           trop,         gradm,        binter,       extkn,         &
           chil,         rho,          tau,                         &

         ! atmospheric forcing
           forc_pco2m,   forc_po2m,    forc_us,      forc_vs,       &
           forc_t,       forc_q,       forc_prc,     forc_prl,      &
           forc_rain,    forc_snow,    forc_psrf,    forc_pbot,     &
           forc_sols,    forc_soll,    forc_solsd,   forc_solld,    &
           forc_frl,     forc_hgt_u,   forc_hgt_t,   forc_hgt_q,    &
           forc_rhoair,                                             &

         ! land surface variables required for restart
           z_soisno,     dz_soisno,    t_soisno,     wliq_soisno,   &
           wice_soisno,  smp,          hk,                          &
           t_grnd,       tleaf,        ldew,     ldew_rain,      ldew_snow,             &
           sag,          scv,          snowdp,       fveg,          &
           fsno,         sigf,         green,        lai,           &
           sai,          alb,          ssun,         ssha,          &
           thermk,       extkb,        extkd,                       &
#ifdef PLANT_HYDRAULIC_STRESS
           vegwp,        gs0sun,       gs0sha,                      &
#endif
#ifdef OzoneStress
           lai_old,      o3uptakesun,  o3uptakesha       ,forc_ozone        , &
#endif
           zwt,          dpond,        wa,                          &
           t_lake,       lake_icefrac, savedtke1,                   &
         
         ! SNICAR snow model related
           snw_rds,      ssno,                                     &
           mss_bcpho,    mss_bcphi,   mss_ocpho,     mss_ocphi,    &
           mss_dst1,     mss_dst2,    mss_dst3,      mss_dst4,     &

         ! additional diagnostic variables for output
           laisun,       laisha,       rootr,                       &
           rstfacsun_out,rstfacsha_out,gssun_out,    gssha_out,     &
#ifdef WUEdiag
           assimsun_out, etrsun_out,   assim_RuBP_sun_out,          & 
           assim_Rubisco_sun_out,      cisun_out,    Dsun_out,      & 
           gammasun_out,lambdasun_out,                              &
           assimsha_out, etrsha_out,   assim_RuBP_sha_out,          &
           assim_Rubisco_sha_out,      cisha_out,    Dsha_out,      &
           gammasha_out, lambdasha_out,lambda_out,                  &
#endif
           h2osoi,       wat,           &

         ! FLUXES
           taux,         tauy,         fsena,        fevpa,         &
           lfevpa,       fsenl,        fevpl,        etr,           &
           fseng,        fevpg,        olrg,         fgrnd,         &
           trad,         tref,         qref,         rsur,          &
           rnof,         qintr,        qinfl,        qdrip,         &
           rst,          assim,        respc,        sabvsun,       &
           sabvsha,      sabg,         sr,           solvd,         &
           solvi,        solnd,        solni,        srvd,          &
           srvi,         srnd,         srni,         solvdln,       &
           solviln,      solndln,      solniln,      srvdln,        &
           srviln,       srndln,       srniln,       qcharge,       &
           xerr,         zerr,                                      &

         ! TUNABLE modle constants
           zlnd,         zsno,         csoilc,       dewmx,         &
           wtfact,       capr,         cnfac,        ssi,           &
           wimp,         pondmx,       smpmax,       smpmin,        &
           trsmx0,       tcrit,                                     & 

         ! additional variables required by coupling with WRF model 
           emis,         z0m,          zol,          rib,           &
           ustar,        qstar,        tstar,        fm,            &
           fh,           fq                                         )

!=======================================================================
!
! Main subroutine, advance time information
!
! Original author : Yongjiu Dai, 09/30/1999; 08/30/2002, 12/2012, 02/2014
!
!    FLOW DIAGRAM FOR CLMMAIN
!
!    CLMMAIN ===> netsolar                 |> all surface
!                 rain_snow_temp           !> all surface
!
!                 LEAF_interception        |]  
!                 newsnow                  |] patchtype = 0 (soil ground)
!                 THERMAL                  |]           = 1 (urban & built-up)
!                 WATER                    |]           = 2 (wetland)
!                 snowcompaction           |]           = 3 (land ice)
!                 snowlayerscombine        |]           = 4 (lake)
!                 snowlayersdivide         |]
!                 snowage                  |]  
!
!                 newsnow_lake             |]
!                 laketem                  |] lake scheme
!                 snowwater_lake           |]
!
!                 SOCEAN                   |> ocean and sea ice
!                
!                 orb_coszen               |> all surface
!                 EcoModel (LAI_empirical) |> land 
!                 snowfraction             |> land
!                 albland                  |> land 
!                 albocean                 |> ocean & sea ice
!
!=======================================================================

  USE precision
  USE GlobalVars
  USE PhysicalConstants, only: tfrz, denh2o, denice
  USE MOD_TimeVariables, only: tlai, tsai
#ifdef PFT_CLASSIFICATION
  USE mod_landpft, only : patch_pft_s, patch_pft_e
  USE MOD_PFTimeInvars
  USE MOD_PFTimeVars
#endif
#ifdef PC_CLASSIFICATION
  USE mod_landpc
  USE MOD_PCTimeInvars
  USE MOD_PCTimeVars
#endif
  USE SOIL_SNOW_hydrology
  USE SNOW_Layers_CombineDivide
  USE GLACIER
  USE LAKE
  USE SIMPLE_OCEAN
  USE ALBEDO
  USE timemanager
  USE mod_namelist, only : DEF_Interception_scheme, DEF_USE_VARIABLY_SATURATED_FLOW
#if(defined CaMa_Flood)
   USE colm_CaMaMod,only:fldfluxes
   USE YOS_CMF_INPUT,      ONLY: LWINFILT,LWEVAP
#endif

  ! SNICAR
  USE AerosolMod,    only : AerosolFluxes, AerosolMasses
  USE SnowSnicarMod, only : SnowAge_grain

  IMPLICIT NONE
 
! ------------------------ Dummy Argument ------------------------------
  REAL(r8),intent(in) :: deltim  !seconds in a time step [second]
  LOGICAL, intent(in) :: doalb   !true if time for surface albedo calculation
  LOGICAL, intent(in) :: dolai   !true if time for leaf area index calculation
  LOGICAL, intent(in) :: dosst   !true to update sst/ice/snow before calculation

  INTEGER, intent(in) :: & 
        ipatch        ! patch index

  REAL(r8), intent(in) :: &
        patchlonr   ,&! logitude in radians
        patchlatr     ! latitude in radians

  INTEGER, intent(in) :: & 
        patchclass  ,&! land cover type of USGS classification or others
        patchtype     ! land water type (0=soil, 1=urban and built-up, 
                      ! 2=wetland, 3=land ice, 4=land water bodies, 99 = ocean)
! Parameters
! ----------------------
  REAL(r8), intent(in) :: &
        lakedepth        ,&! lake depth (m)
        dz_lake(nl_lake) ,&! lake layer thickness (m)

        ! soil physical parameters and lake info
        soil_s_v_alb     ,&! albedo of visible of the saturated soil
        soil_d_v_alb     ,&! albedo of visible of the dry soil
        soil_s_n_alb     ,&! albedo of near infrared of the saturated soil
        soil_d_n_alb     ,&! albedo of near infrared of the dry soil

        vf_quartz (nl_soil), & ! volumetric fraction of quartz within mineral soil
        vf_gravels(nl_soil), &! volumetric fraction of gravels
        vf_om     (nl_soil), &! volumetric fraction of organic matter
        vf_sand   (nl_soil), &! volumetric fraction of sand
        wf_gravels(nl_soil), &! gravimetric fraction of gravels
        wf_sand   (nl_soil), &! gravimetric fraction of sand
        porsl(nl_soil)   ,&! fraction of soil that is voids [-]
        psi0(nl_soil)    ,&! minimum soil suction [mm]
        bsw(nl_soil)     ,&! clapp and hornbereger "b" parameter [-]
#ifdef vanGenuchten_Mualem_SOIL_MODEL
        theta_r  (1:nl_soil), &
        alpha_vgm(1:nl_soil), &
        n_vgm    (1:nl_soil), &
        L_vgm    (1:nl_soil), &
        sc_vgm   (1:nl_soil), &
        fc_vgm   (1:nl_soil), &
#endif
        hksati(nl_soil)  ,&! hydraulic conductivity at saturation [mm h2o/s]
        csol(nl_soil)    ,&! heat capacity of soil solids [J/(m3 K)]
        k_solids(nl_soil),&! thermal conductivity of minerals soil [W/m-K]
        dksatu(nl_soil)  ,&! thermal conductivity of saturated unfrozen soil [W/m-K]
        dksatf(nl_soil)  ,&! thermal conductivity of saturated frozen soil [W/m-K]
        dkdry(nl_soil)   ,&! thermal conductivity for dry soil  [J/(K s m)]
#ifdef THERMAL_CONDUCTIVITY_SCHEME_4 
        BA_alpha(nl_soil),&! alpha in Balland and Arp(2005) thermal conductivity scheme
        BA_beta (nl_soil),&! beta in Balland and Arp(2005) thermal conductivity scheme
#endif
        rootfr(nl_soil)  ,&! fraction of roots in each soil layer

        ! vegetation static, dynamic, derived parameters
        htop        ,&! canopy top height [m]
        hbot        ,&! canopy bottom height [m]
        sqrtdi      ,&! inverse sqrt of leaf dimension [m**-0.5]
        effcon      ,&! quantum efficiency of RuBP regeneration (mol CO2/mol quanta)
        vmax25      ,&! maximum carboxylation rate at 25 C at canopy top
#ifdef PLANT_HYDRAULIC_STRESS
        kmax_sun    ,&
        kmax_sha    ,&
        kmax_xyl    ,&
        kmax_root   ,&
        psi50_sun   ,&! water potential at 50% loss of sunlit leaf tissue conductance (mmH2O)
        psi50_sha   ,&! water potential at 50% loss of shaded leaf tissue conductance (mmH2O)
        psi50_xyl   ,&! water potential at 50% loss of xylem tissue conductance (mmH2O)
        psi50_root  ,&! water potential at 50% loss of root tissue conductance (mmH2O)
        ck          ,&! shape-fitting parameter for vulnerability curve (-)
#endif
        slti        ,&! slope of low temperature inhibition function      [s3] 
        hlti        ,&! 1/2 point of low temperature inhibition function  [s4]
        shti        ,&! slope of high temperature inhibition function     [s1]
        hhti        ,&! 1/2 point of high temperature inhibition function [s2]
        trda        ,&! temperature coefficient in gs-a model             [s5]
        trdm        ,&! temperature coefficient in gs-a model             [s6]
        trop        ,&! temperature coefficient in gs-a model          
        gradm       ,&! conductance-photosynthesis slope parameter
        binter      ,&! conductance-photosynthesis intercep
        extkn       ,&! coefficient of leaf nitrogen allocation
        chil        ,&! leaf angle distribution factor
        rho(2,2)    ,&! leaf reflectance (iw=iband, il=life and dead)
        tau(2,2)    ,&! leaf transmittance (iw=iband, il=life and dead)

        ! tunable parameters
        zlnd        ,&! roughness length for soil [m]
        zsno        ,&! roughness length for snow [m]
        csoilc      ,&! drag coefficient for soil under canopy [-]
        dewmx       ,&! maximum dew
        wtfact      ,&! fraction of model area with high water table
        capr        ,&! tuning factor to turn first layer T into surface T
        cnfac       ,&! Crank Nicholson factor between 0 and 1
        ssi         ,&! irreducible water saturation of snow
        wimp        ,&! water impremeable if porosity less than wimp
        pondmx      ,&! ponding depth (mm)
        smpmax      ,&! wilting point potential in mm
        smpmin      ,&! restriction for min of soil poten.  (mm)
        trsmx0      ,&! max transpiration for moist soil+100% veg.  [mm/s]
        tcrit         ! critical temp. to determine rain or snow

! Forcing
! ----------------------
  REAL(r8), intent(in) :: &
        forc_pco2m  ,&! partial pressure of CO2 at observational height [pa]
        forc_po2m   ,&! partial pressure of O2 at observational height [pa]
        forc_us     ,&! wind speed in eastward direction [m/s]
        forc_vs     ,&! wind speed in northward direction [m/s]
        forc_t      ,&! temperature at agcm reference height [kelvin]
        forc_q      ,&! specific humidity at agcm reference height [kg/kg]
        forc_prc    ,&! convective precipitation [mm/s]
        forc_prl    ,&! large scale precipitation [mm/s]
        forc_psrf   ,&! atmosphere pressure at the surface [pa]
        forc_pbot   ,&! atmosphere pressure at the bottom of the atmos. model level [pa]
        forc_sols   ,&! atm vis direct beam solar rad onto srf [W/m2]
        forc_soll   ,&! atm nir direct beam solar rad onto srf [W/m2]
        forc_solsd  ,&! atm vis diffuse solar rad onto srf [W/m2]
        forc_solld  ,&! atm nir diffuse solar rad onto srf [W/m2]
        forc_frl    ,&! atmospheric infrared (longwave) radiation [W/m2]
        forc_hgt_u  ,&! observational height of wind [m]
        forc_hgt_t  ,&! observational height of temperature [m]
        forc_hgt_q  ,&! observational height of humidity [m]
        forc_rhoair   ! density air [kg/m3]
#if(defined CaMa_Flood)
   real(r8), intent(in)    :: fldfrc   
   real(r8), intent(inout) :: flddepth  !effective inundation depth:treat as precipitation--> allow re-evaporation and infiltrition![mm/s]
   real(r8), intent(out)   :: fevpg_fld
   real(r8), intent(out)   :: qinfl_fld
#endif
! Variables required for restart run
! ----------------------------------------------------------------------
  INTEGER, intent(in) :: &
        idate(3)      ! next time-step /year/julian day/second in a day/

  REAL(r8), intent(inout) :: oro  ! ocean(0)/seaice(2)/ flag
  REAL(r8), intent(inout) :: &
        z_soisno(maxsnl+1:nl_soil)    ,&! layer depth (m)
        dz_soisno(maxsnl+1:nl_soil)   ,&! layer thickness (m)
        t_soisno(maxsnl+1:nl_soil)    ,&! soil + snow layer temperature [K]
        wliq_soisno(maxsnl+1:nl_soil) ,&! liquid water (kg/m2)
        wice_soisno(maxsnl+1:nl_soil) ,&! ice lens (kg/m2)
        hk(1:nl_soil)                 ,&! hydraulic conductivity [mm h2o/s]
        smp(1:nl_soil)                ,&! soil matrix potential [mm]

        t_lake(nl_lake)       ,&! lake temperature (kelvin)
        lake_icefrac(nl_lake) ,&! lake mass fraction of lake layer that is frozen
        savedtke1             ,&! top level eddy conductivity (W/m K)
#ifdef PLANT_HYDRAULIC_STRESS 
        vegwp(nvegwcs)        ,&! ground surface temperature [k]
        gs0sun                ,&! working copy of sunlit stomata conductance
        gs0sha                ,&! working copy of shalit stomata conductance
#endif
#ifdef OzoneStress
        lai_old    ,&! lai in last time step
        o3uptakesun,&! Ozone does, sunlit leaf (mmol O3/m^2)
        o3uptakesha,&! Ozone does, shaded leaf (mmol O3/m^2)
        forc_ozone ,&
#endif
        t_grnd      ,&! ground surface temperature [k]
        tleaf       ,&! leaf temperature [K]
        ldew        ,&! depth of water on foliage [kg/m2/s]
        ldew_rain   ,&
        ldew_snow   ,&
        sag         ,&! non dimensional snow age [-]
        scv         ,&! snow mass (kg/m2)
        snowdp      ,&! snow depth (m)
        zwt         ,&! the depth to water table [m]
        dpond       ,&! depth of ponding water [mm]
        wa          ,&! water storage in aquifer [mm]

        snw_rds   ( maxsnl+1:0 ) ,&! effective grain radius (col,lyr) [microns, m-6]
        mss_bcpho ( maxsnl+1:0 ) ,&! mass of hydrophobic BC in snow  (col,lyr) [kg]
        mss_bcphi ( maxsnl+1:0 ) ,&! mass of hydrophillic BC in snow (col,lyr) [kg]
        mss_ocpho ( maxsnl+1:0 ) ,&! mass of hydrophobic OC in snow  (col,lyr) [kg]
        mss_ocphi ( maxsnl+1:0 ) ,&! mass of hydrophillic OC in snow (col,lyr) [kg]
        mss_dst1  ( maxsnl+1:0 ) ,&! mass of dust species 1 in snow  (col,lyr) [kg]
        mss_dst2  ( maxsnl+1:0 ) ,&! mass of dust species 2 in snow  (col,lyr) [kg]
        mss_dst3  ( maxsnl+1:0 ) ,&! mass of dust species 3 in snow  (col,lyr) [kg]
        mss_dst4  ( maxsnl+1:0 ) ,&! mass of dust species 4 in snow  (col,lyr) [kg]
        ssno    (2,2,maxsnl+1:1) ,&! snow layer absorption [-]

        fveg        ,&! fraction of vegetation cover
        fsno        ,&! fractional snow cover
        sigf        ,&! fraction of veg cover, excluding snow-covered veg [-]
        green       ,&! greenness
        lai         ,&! leaf area index
        sai         ,&! stem area index
 
        coszen      ,&! cosine of solar zenith angle
        alb(2,2)    ,&! averaged albedo [-]
        ssun(2,2)   ,&! sunlit canopy absorption for solar radiation
        ssha(2,2)   ,&! shaded canopy absorption for solar radiation
        thermk      ,&! canopy gap fraction for tir radiation
        extkb       ,&! (k, g(mu)/mu) direct solar extinction coefficient
        extkd         ! diffuse and scattered diffuse PAR extinction coefficient
        
     
! additional diagnostic variables for output
  REAL(r8), intent(out) :: &
        laisun      ,&! sunlit leaf area index
        laisha      ,&! shaded leaf area index
        rstfacsun_out,&! factor of soil water stress
        rstfacsha_out,&! factor of soil water stress
        gssun_out,    &! sunlit stomata conductance
        gssha_out,    &! shaded stomata conductance
        wat         ,&! total water storage
        rootr(nl_soil),&! water exchange between soil and root. Positive: soil->root [?]
        h2osoi(nl_soil) ! volumetric soil water in layers [m3/m3]

#ifdef WUEdiag
  REAL(r8), intent(out) :: &
       assimsun_out           ,&
       etrsun_out             ,&
       assim_RuBP_sun_out     ,&
       assim_Rubisco_sun_out  ,&
       cisun_out              ,&
       Dsun_out               ,&
       gammasun_out           ,&
       lambdasun_out          ,&
       assimsha_out           ,&
       etrsha_out             ,&
       assim_RuBP_sha_out     ,&
       assim_Rubisco_sha_out  ,&
       cisha_out              ,&
       Dsha_out               ,&
       gammasha_out           ,&
       lambdasha_out          ,&
       lambda_out             
#endif
! Fluxes
! ----------------------------------------------------------------------
  REAL(r8), intent(out) :: &
        taux        ,&! wind stress: E-W [kg/m/s**2]
        tauy        ,&! wind stress: N-S [kg/m/s**2]
        fsena       ,&! sensible heat from canopy height to atmosphere [W/m2]
        fevpa       ,&! evapotranspiration from canopy height to atmosphere [mm/s]
        lfevpa      ,&! latent heat flux from canopy height to atmosphere [W/2]
        fsenl       ,&! ensible heat from leaves [W/m2]
        fevpl       ,&! evaporation+transpiration from leaves [mm/s]
        etr         ,&! transpiration rate [mm/s]
        fseng       ,&! sensible heat flux from ground [W/m2]
        fevpg       ,&! evaporation heat flux from ground [mm/s]
        olrg        ,&! outgoing long-wave radiation from ground+canopy
        fgrnd       ,&! ground heat flux [W/m2]
        xerr        ,&! water balance error at current time-step [mm/s]
        zerr        ,&! energy balnce errore at current time-step [W/m2]

        tref        ,&! 2 m height air temperature [K]
        qref        ,&! 2 m height air specific humidity
        trad        ,&! radiative temperature [K]
        rsur        ,&! surface runoff (mm h2o/s)
        rnof        ,&! total runoff (mm h2o/s)
        qintr       ,&! interception (mm h2o/s)
        qinfl       ,&! inflitration (mm h2o/s)
        qdrip       ,&! throughfall (mm h2o/s)
        qcharge     ,&! groundwater recharge [mm/s]
       
        rst         ,&! canopy stomatal resistance 
        assim       ,&! canopy assimilation
        respc       ,&! canopy respiration

        sabvsun     ,&! solar absorbed by sunlit vegetation [W/m2]
        sabvsha     ,&! solar absorbed by shaded vegetation [W/m2]
        sabg        ,&! solar absorbed by ground  [W/m2]
        sr          ,&! total reflected solar radiation (W/m2)
        solvd       ,&! incident direct beam vis solar radiation (W/m2)
        solvi       ,&! incident diffuse beam vis solar radiation (W/m2)
        solnd       ,&! incident direct beam nir solar radiation (W/m2)
        solni       ,&! incident diffuse beam nir solar radiation (W/m2)
        srvd        ,&! reflected direct beam vis solar radiation (W/m2)
        srvi        ,&! reflected diffuse beam vis solar radiation (W/m2)
        srnd        ,&! reflected direct beam nir solar radiation (W/m2)
        srni        ,&! reflected diffuse beam nir solar radiation (W/m2)
        solvdln     ,&! incident direct beam vis solar radiation at local noon(W/m2)
        solviln     ,&! incident diffuse beam vis solar radiation at local noon(W/m2)
        solndln     ,&! incident direct beam nir solar radiation at local noon(W/m2)
        solniln     ,&! incident diffuse beam nir solar radiation at local noon(W/m2)
        srvdln      ,&! reflected direct beam vis solar radiation at local noon(W/m2)
        srviln      ,&! reflected diffuse beam vis solar radiation at local noon(W/m2)
        srndln      ,&! reflected direct beam nir solar radiation at local noon(W/m2)
        srniln      ,&! reflected diffuse beam nir solar radiation at local noon(W/m2)

        forc_rain   ,&! rain [mm/s]
        forc_snow   ,&! snow [mm/s]

        emis        ,&! averaged bulk surface emissivity
        z0m         ,&! effective roughness [m]
        zol         ,&! dimensionless height (z/L) used in Monin-Obukhov theory
        rib         ,&! bulk Richardson number in surface layer
        ustar       ,&! u* in similarity theory [m/s]
        qstar       ,&! q* in similarity theory [kg/kg]
        tstar       ,&! t* in similarity theory [K]
        fm          ,&! integral of profile function for momentum
        fh          ,&! integral of profile function for heat
        fq            ! integral of profile function for moisture

! ----------------------- Local  Variables -----------------------------
   REAL(r8) :: &
        calday      ,&! Julian cal day (1.xx to 365.xx)
        endwb       ,&! water mass at the end of time step
        errore      ,&! energy balnce errore (Wm-2)
        errorw      ,&! water balnce errore (mm)
        fiold(maxsnl+1:nl_soil), &! fraction of ice relative to the total water
        w_old       ,&! liquid water mass of the column at the previous time step (mm)
        orb_coszen  ,&! cosine of the solar zenith angle
        ! sabvg       ,&! solar absorbed by ground + vegetation [W/m2]
        parsun      ,&! PAR by sunlit leaves [W/m2]
        parsha      ,&! PAR by shaded leaves [W/m2]
        qseva       ,&! ground surface evaporation rate (mm h2o/s)
        qsdew       ,&! ground surface dew formation (mm h2o /s) [+]
        qsubl       ,&! sublimation rate from snow pack (mm h2o /s) [+]
        qfros       ,&! surface dew added to snow pack (mm h2o /s) [+]
        scvold      ,&! snow cover for previous time step [mm]
        sm          ,&! rate of snowmelt [kg/(m2 s)]
        ssw         ,&! water volumetric content of soil surface layer [m3/m3]
        tssub(7)    ,&! surface/sub-surface temperatures [K]
        tssea       ,&! sea surface temperature [K]
        totwb       ,&! water mass at the begining of time step
        wt          ,&! fraction of vegetation buried (covered) by snow [-]
        zi_soisno(maxsnl:nl_soil) ! interface level below a "z" level (m)

   REAL(r8) :: &
        prc_rain    ,&! convective rainfall [kg/(m2 s)]
        prc_snow    ,&! convective snowfall [kg/(m2 s)]
        prl_rain    ,&! large scale rainfall [kg/(m2 s)]
        prl_snow    ,&! large scale snowfall [kg/(m2 s)]
        t_precip    ,&! snowfall/rainfall temperature [kelvin]
        bifall      ,&! bulk density of newly fallen dry snow [kg/m3]
        pg_rain     ,&! rainfall onto ground including canopy runoff [kg/(m2 s)]
        pg_snow     ,&! snowfall onto ground including canopy runoff [kg/(m2 s)]
        qintr_rain  ,&! rainfall interception (mm h2o/s)
        qintr_snow  ,&! snowfall interception (mm h2o/s)
        errw_rsub     ! the possible subsurface runoff deficit after PHS is included

  INTEGER snl       ,&! number of snow layers
        imelt(maxsnl+1:nl_soil), &! flag for: melting=1, freezing=2, Nothing happended=0
        lb          , lbsn, &! lower bound of arrays
        j             ! do looping index

      ! For SNICAR snow model
      !----------------------------------------------------------------------
      LOGICAL  do_capsnow              !true => do snow capping
      INTEGER  snl_bef                 !number of snow layers
      REAL(r8) snwcp_ice               !excess precipitation due to snow capping [kg m-2 s-1]
      REAL(r8) forc_aer        ( 14 )  !aerosol deposition from atmosphere model (grd,aer) [kg m-1 s-1]
      REAL(r8) snofrz    (maxsnl+1:0)  !snow freezing rate (col,lyr) [kg m-2 s-1]
      REAL(r8) t_soisno_ (maxsnl+1:1)  !soil + snow layer temperature [K]
      REAL(r8) dz_soisno_(maxsnl+1:1)  !layer thickness (m)
      REAL(r8) sabg_lyr  (maxsnl+1:1)  !snow layer absorption [W/m-2]

      REAL(r8) mss_cnc_bcphi ( maxsnl+1:0 )     !mass concentration of hydrophilic BC (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_bcpho ( maxsnl+1:0 )     !mass concentration of hydrophobic BC (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_ocphi ( maxsnl+1:0 )     !mass concentration of hydrophilic OC (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_ocpho ( maxsnl+1:0 )     !mass concentration of hydrophobic OC (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_dst1  ( maxsnl+1:0 )     !mass concentration of dust aerosol species 1 (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_dst2  ( maxsnl+1:0 )     !mass concentration of dust aerosol species 2 (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_dst3  ( maxsnl+1:0 )     !mass concentration of dust aerosol species 3 (col,lyr) [kg/kg]
      REAL(r8) mss_cnc_dst4  ( maxsnl+1:0 )     !mass concentration of dust aerosol species 4 (col,lyr) [kg/kg]
      !----------------------------------------------------------------------

     REAL(r8) :: a, aa 
     INTEGER ps, pe, pc
      
!======================================================================
#if(defined CaMa_Flood)
      real(r8) :: &
      kk, &
      taux_fld,     &! wind stress: E-W [kg/m/s**2]
      tauy_fld,     &! wind stress: N-S [kg/m/s**2]
      fsena_fld,    &! sensible heat from agcm reference height to atmosphere [W/m2]
      fevpa_fld,    &! evaporation from agcm reference height to atmosphere [mm/s]
      fseng_fld,    &! sensible heat flux from ground [W/m2]
      tref_fld,     &! 2 m height air temperature [kelvin]
      qref_fld,     &! 2 m height air humidity
      z0m_fld,      &! effective roughness [m]
      zol_fld,      &! dimensionless height (z/L) used in Monin-Obukhov theory
      rib_fld,      &! bulk Richardson number in surface layer
      ustar_fld,    &! friction velocity [m/s]
      tstar_fld,    &! temperature scaling parameter
      qstar_fld,    &! moisture scaling parameter
      fm_fld,       &! integral of profile function for momentum
      fh_fld,       &! integral of profile function for heat
      fq_fld !,       &        !,       & !,       &! integral of profile function for moisture
#endif

!======================================================================
!  [1] Solar absorbed by vegetation and ground
!      and precipitation information (rain/snow fall and precip temperature
!======================================================================

     ! SNICAR
     do_capsnow  = .false.   !true => DO snow capping
     snwcp_ice   = 0.0       !excess precipitation due to snow capping [kg m-2 s-1]
     snofrz(:)   = 0.        !snow freezing rate (col,lyr) [kg m-2 s-1]
     forc_aer(:) = 4.2E-7    !aerosol deposition from atmosphere model (grd,aer) [kg m-1 s-1]
     !forc_aer(:) = 0.        !aerosol deposition from atmosphere model (grd,aer) [kg m-1 s-1]

      CALL netsolar (ipatch,idate,deltim,patchlonr,patchtype,&
                     forc_sols,forc_soll,forc_solsd,forc_solld,&
                     alb,ssun,ssha,lai,sai,rho,tau,ssno,&
                     parsun,parsha,sabvsun,sabvsha,sabg,sabg_lyr,sr,&
                     solvd,solvi,solnd,solni,srvd,srvi,srnd,srni,&
                     solvdln,solviln,solndln,solniln,srvdln,srviln,srndln,srniln)

      CALL rain_snow_temp (patchtype, &
                           forc_t,forc_q,forc_psrf,forc_prc,forc_prl,tcrit,&
                           prc_rain,prc_snow,prl_rain,prl_snow,t_precip,bifall)

      forc_rain = prc_rain + prl_rain
      forc_snow = prc_snow + prl_snow

!======================================================================

                         !         / SOIL GROUND          (patchtype = 0)
IF (patchtype <= 2) THEN ! <=== is - URBAN and BUILT-UP   (patchtype = 1)
                         !         \ WETLAND              (patchtype = 2)

! NOTE: PFT and PC are only for soil patches, i.e., patchtype=0.
!======================================================================
                         !initial set
      scvold = scv       !snow mass at previous time step

      snl = 0
      DO j=maxsnl+1,0
         IF(wliq_soisno(j)+wice_soisno(j)>0.) snl=snl-1
      ENDDO

      zi_soisno(0)=0.
      IF (snl < 0) THEN
      DO j = -1, snl, -1
         zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
      ENDDO
      ENDIF
      DO j = 1,nl_soil
         zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
      ENDDO

      totwb = ldew + scv + sum(wice_soisno(1:)+wliq_soisno(1:)) + wa
      
      IF (DEF_USE_VARIABLY_SATURATED_FLOW) THEN
         totwb = totwb + dpond
      ENDIF

      fiold(:) = 0.0
      IF (snl <0 ) THEN
         fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
      ENDIF

!----------------------------------------------------------------------
! [2] Canopy interception and precipitation onto ground surface
!----------------------------------------------------------------------

IF (patchtype == 0) THEN

#if(defined USGS_CLASSIFICATION || defined IGBP_CLASSIFICATION)
if (DEF_Interception_scheme==1) then
   CALL LEAF_interception_colm (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
                              
ELSEIF (DEF_Interception_scheme==2) then
   CALL LEAF_interception_clm4 (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
ELSEIF (DEF_Interception_scheme==3) then
   CALL LEAF_interception_clm5 (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
ELSEIF (DEF_Interception_scheme==4) then
   CALL LEAF_interception_noahmp (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
ELSEIF  (DEF_Interception_scheme==5) then
   CALL LEAF_interception_matsiro (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)

ELSEIF  (DEF_Interception_scheme==6) then
   CALL LEAF_interception_vic (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
endif

#endif

#ifdef PFT_CLASSIFICATION
      CALL LEAF_interception_pftwrap (ipatch,deltim,dewmx,forc_us,forc_vs,forc_t,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
                            
#endif

#ifdef PC_CLASSIFICATION
      CALL LEAF_interception_pcwrap (ipatch,deltim,dewmx,forc_us,forc_vs,forc_t,chil,&
                              prc_rain,prc_snow,prl_rain,prl_snow,&
                              ldew,ldew_rain,ldew_snow,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
#endif

ELSE
   if (DEF_Interception_scheme==1) then
      CALL LEAF_interception_colm (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
                                 
   ELSEIF (DEF_Interception_scheme==2) then
      CALL LEAF_interception_clm4 (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
   ELSEIF (DEF_Interception_scheme==3) then
      CALL LEAF_interception_clm5 (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
   ELSEIF (DEF_Interception_scheme==4) then
      CALL LEAF_interception_noahmp (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
   ELSEIF  (DEF_Interception_scheme==5) then
      CALL LEAF_interception_matsiro (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
   
   ELSEIF  (DEF_Interception_scheme==6) then
      CALL LEAF_interception_vic (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tref, tleaf,&
                                 prc_rain,prc_snow,prl_rain,prl_snow,&
                                 ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
   endif   

   !   CALL LEAF_interception (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,tleaf,&
   !                           prc_rain,prc_snow,prl_rain,prl_snow,&
   !                           ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,pg_snow,qintr,qintr_rain,qintr_snow)
ENDIF
      
      qdrip = pg_rain + pg_snow

!----------------------------------------------------------------------
! [3] Initilize new snow nodes for snowfall / sleet
!----------------------------------------------------------------------

      snl_bef = snl

      CALL newsnow (patchtype,maxsnl,deltim,t_grnd,pg_rain,pg_snow,bifall,&
                    t_precip,zi_soisno(:0),z_soisno(:0),dz_soisno(:0),t_soisno(:0),&
                    wliq_soisno(:0),wice_soisno(:0),fiold(:0),snl,sag,scv,snowdp,fsno)

      ! new snow layer
      IF (snl .lt. snl_bef) THEN
         sabg_lyr(snl+1:snl-snl_bef+1) = sabg_lyr(snl_bef+1:1)
         sabg_lyr(snl-snl_bef+2:1) = 0.
      ENDIF

!----------------------------------------------------------------------
! [4] Energy and Water balance 
!----------------------------------------------------------------------
      lb  = snl + 1           !lower bound of array 
      lbsn = min(lb,0)

      CALL THERMAL (ipatch   ,patchtype         ,lb                ,deltim            ,&
           trsmx0            ,zlnd              ,zsno              ,csoilc            ,&
           dewmx             ,capr              ,cnfac             ,vf_quartz         ,&
           vf_gravels        ,vf_om             ,vf_sand           ,wf_gravels        ,&
           wf_sand           ,csol              ,porsl             ,psi0              ,&
#ifdef Campbell_SOIL_MODEL
           bsw               ,                                                         &
#endif
#ifdef vanGenuchten_Mualem_SOIL_MODEL
           theta_r           ,alpha_vgm         ,n_vgm             ,L_vgm             ,&
           sc_vgm            ,fc_vgm            ,                                      &
#endif
           k_solids          ,dksatu            ,dksatf            ,dkdry             ,&
#ifdef THERMAL_CONDUCTIVITY_SCHEME_4
           BA_alpha          ,BA_beta                                                 ,&
#endif
           lai               ,laisun            ,laisha                               ,&
           sai               ,htop              ,hbot              ,sqrtdi            ,&
           rootfr            ,rstfacsun_out     ,rstfacsha_out     ,&
           gssun_out         ,gssha_out         ,&
#ifdef WUEdiag
           assimsun_out      ,etrsun_out        ,assim_RuBP_sun_out                   ,& 
           assim_Rubisco_sun_out                ,cisun_out         ,Dsun_out          ,& 
           gammasun_out      ,lambdasun_out     ,&
           assimsha_out      ,etrsha_out        ,assim_RuBP_sha_out,&
           assim_Rubisco_sha_out                ,cisha_out         ,Dsha_out          ,&
           gammasha_out      ,lambdasha_out     ,lambda_out        ,& 
#endif
           effcon            ,&
           vmax25            ,hksati            ,smp               ,hk                ,&
#ifdef PLANT_HYDRAULIC_STRESS
           kmax_sun          ,kmax_sha          ,kmax_xyl          ,kmax_root         ,&  
           psi50_sun         ,psi50_sha         ,psi50_xyl         ,psi50_root        ,&  
           ck                ,vegwp             ,gs0sun            ,gs0sha            ,&
#endif
#ifdef OzoneStress
           lai_old           ,o3uptakesun       ,o3uptakesha       ,forc_ozone        , &
#endif
           slti              ,hlti              ,shti              ,hhti              ,&
           trda              ,trdm              ,trop              ,gradm             ,&
           binter            ,extkn             ,forc_hgt_u        ,forc_hgt_t        ,&
           forc_hgt_q        ,forc_us           ,forc_vs           ,forc_t            ,&
           forc_q            ,forc_rhoair       ,forc_psrf         ,forc_pco2m        ,&
           forc_po2m         ,coszen            ,parsun            ,parsha            ,&
           sabvsun           ,sabvsha           ,sabg              ,forc_frl          ,&
           extkb             ,extkd             ,thermk            ,fsno              ,&
           sigf              ,dz_soisno(lb:)    ,z_soisno(lb:)     ,zi_soisno(lb-1:)  ,&
           tleaf             ,t_soisno(lb:)     ,wice_soisno(lb:)  ,wliq_soisno(lb:)  ,&
           ldew, ldew_rain, ldew_snow,    scv         ,snowdp      ,imelt(lb:)      ,&
           taux              ,tauy              ,fsena             ,fevpa             ,&
           lfevpa            ,fsenl             ,fevpl             ,etr               ,&
           fseng             ,fevpg             ,olrg              ,fgrnd             ,&
           rootr             ,qseva             ,qsdew             ,qsubl             ,&
           qfros             ,sm                ,tref              ,qref              ,&
           trad              ,rst               ,assim             ,respc             ,&
           errore            ,emis              ,z0m               ,zol               ,&
           rib               ,ustar             ,qstar             ,tstar             ,&
           fm                ,fh                ,fq                ,pg_rain           ,&
           pg_snow           ,t_precip          ,qintr_rain        ,qintr_snow        ,&
           snofrz(lbsn:0), sabg_lyr(lb:1)                                        )

      IF (.not. DEF_USE_VARIABLY_SATURATED_FLOW) THEN

         CALL WATER (ipatch     ,patchtype         ,lb                ,nl_soil           ,&
              deltim            ,z_soisno(lb:)     ,dz_soisno(lb:)    ,zi_soisno(lb-1:)  ,&
              bsw               ,porsl             ,psi0              ,hksati            ,&
              rootr             ,t_soisno(lb:)     ,wliq_soisno(lb:)  ,wice_soisno(lb:)  ,smp,hk,&
              pg_rain           ,sm                ,etr               ,qseva             ,&
              qsdew             ,qsubl             ,qfros             ,rsur              ,&
              rnof              ,qinfl             ,wtfact            ,pondmx            ,&
              ssi               ,wimp              ,smpmin            ,zwt               ,&
              wa                ,qcharge           ,errw_rsub &

#if(defined CaMa_Flood)
            ,flddepth,fldfrc,qinfl_fld  &
#endif
#ifdef SNICAR
             ,forc_aer          ,&
             mss_bcpho(lbsn:0)   ,mss_bcphi(lbsn:0)   ,mss_ocpho(lbsn:0)   ,mss_ocphi(lbsn:0)   ,&
             mss_dst1(lbsn:0)    ,mss_dst2(lbsn:0)    ,mss_dst3(lbsn:0)    ,mss_dst4(lbsn:0)     &
#endif
              )
      ELSE

         CALL WATER_VSF (ipatch ,patchtype         ,lb                ,nl_soil           ,&
              deltim            ,z_soisno(lb:)     ,dz_soisno(lb:)    ,zi_soisno(lb-1:)  ,&
#ifdef Campbell_SOIL_MODEL
              bsw               ,                                                         &
#endif
#ifdef vanGenuchten_Mualem_SOIL_MODEL
              theta_r           ,alpha_vgm         ,n_vgm             ,L_vgm             ,&
              sc_vgm            ,fc_vgm            ,                                      &
#endif
              porsl             ,psi0              ,hksati            ,                   &
              rootr             ,t_soisno(lb:)     ,wliq_soisno(lb:)  ,wice_soisno(lb:)  ,smp,hk,&
              pg_rain           ,sm                ,etr               ,qseva             ,&
              qsdew             ,qsubl             ,qfros             ,rsur              ,&
              rnof              ,qinfl             ,wtfact            ,ssi               ,&
              pondmx,                                                                     & 
              wimp              ,zwt               ,dpond             ,wa                ,&
              qcharge           ,errw_rsub &
#if(defined CaMa_Flood)
             ,flddepth,fldfrc,qinfl_fld  &
#endif
#ifdef SNICAR
             ,forc_aer         ,&
             mss_bcpho(lbsn:0)   ,mss_bcphi(lbsn:0)   ,mss_ocpho(lbsn:0)   ,mss_ocphi(lbsn:0)   ,&
             mss_dst1(lbsn:0)    ,mss_dst2(lbsn:0)    ,mss_dst3(lbsn:0)    ,mss_dst4(lbsn:0)     &
#endif
             )

      ENDIF

      IF (snl < 0) THEN
         ! Compaction rate for snow 
         ! Natural compaction and metamorphosis. The compaction rate
         ! is recalculated for every new timestep
         lb  = snl + 1   !lower bound of array 
         CALL snowcompaction (lb,deltim,&
                         imelt(lb:0),fiold(lb:0),t_soisno(lb:0),&
                         wliq_soisno(lb:0),wice_soisno(lb:0),dz_soisno(lb:0))

         ! Combine thin snow elements
         lb = maxsnl + 1
#ifdef SNICAR
         CALL snowlayerscombine_snicar (lb,snl,&
                         z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                         wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp,&
                         mss_bcpho(lb:0), mss_bcphi(lb:0), mss_ocpho(lb:0), mss_ocphi(lb:0),&
                         mss_dst1(lb:0), mss_dst2(lb:0), mss_dst3(lb:0), mss_dst4(lb:0) )
#else
         CALL snowlayerscombine (lb,snl,&
                         z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                         wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp)
#endif

         ! Divide thick snow elements
         IF(snl<0) &
#ifdef SNICAR
         CALL snowlayersdivide_snicar (lb,snl,&
                         z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                         wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0),&
                         mss_bcpho(lb:0), mss_bcphi(lb:0), mss_ocpho(lb:0), mss_ocphi(lb:0),&
                         mss_dst1(lb:0), mss_dst2(lb:0), mss_dst3(lb:0), mss_dst4(lb:0) )
#else
         CALL snowlayersdivide (lb,snl,&
                         z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                         wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0))
#endif
      ENDIF
      
      ! Set zero to the empty node
      IF (snl > maxsnl) THEN
         wice_soisno(maxsnl+1:snl) = 0.
         wliq_soisno(maxsnl+1:snl) = 0.
         t_soisno   (maxsnl+1:snl) = 0.
         z_soisno   (maxsnl+1:snl) = 0.
         dz_soisno  (maxsnl+1:snl) = 0.
      ENDIF

      lb = snl + 1
      !TODO: double check
      t_grnd = t_soisno(lb)
      !t_grnd = fsno*t_soisno(lb) + (1.0-fsno)*t_soisno(1)

      ! ----------------------------------------
      ! energy balance
      ! ----------------------------------------
      zerr=errore
#if(defined CLMDEBUG)
      IF (abs(errore) > .5) THEN
         write(6,*) 'Warning: energy balance violation ',errore,patchclass
      ENDIF
#endif

      ! ----------------------------------------
      ! water balance
      ! ----------------------------------------
      endwb=sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv + wa

      IF (DEF_USE_VARIABLY_SATURATED_FLOW) THEN
         endwb = endwb + dpond
      ENDIF

      errorw=(endwb-totwb)-(forc_prc+forc_prl-fevpa-rnof-errw_rsub)*deltim
      IF(patchtype==2) errorw=0.    !wetland
#if(defined CaMa_Flood)
if (LWINFILT) then 
   if (patchtype == 0) then
      errorw=(endwb-totwb)-(forc_prc+forc_prl+qinfl_fld-fevpa-rnof-errw_rsub)*deltim
   ENDIF
ENDIF
#endif
      xerr=errorw/deltim

#if(defined CLMDEBUG)
      IF (abs(errorw) > 1.e-3) THEN
         write(6,*) 'Warning: water balance violation', errorw,patchclass
         !stop
      ENDIF
      if(abs(errw_rsub*deltim)>1.e-3) then
         write(6,*) 'Subsurface runoff deficit due to PHS', errw_rsub*deltim
      end if
#endif

!======================================================================

ELSE IF(patchtype == 3)THEN   ! <=== is LAND ICE (glacier/ice sheet) (patchtype = 3) 

!======================================================================
                            !initial set
      scvold = scv          !snow mass at previous time step

      snl = 0
      DO j=maxsnl+1,0
         IF(wliq_soisno(j)+wice_soisno(j)>0.) snl=snl-1
      ENDDO

      zi_soisno(0)=0.
      IF (snl < 0) THEN
      DO j = -1, snl, -1
         zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
      ENDDO
      ENDIF
      DO j = 1,nl_soil
         zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
      ENDDO

      totwb = scv + sum(wice_soisno(1:)+wliq_soisno(1:))
      fiold(:) = 0.0
      IF (snl <0 ) THEN
         fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
      ENDIF

      pg_rain = prc_rain + prl_rain
      pg_snow = prc_snow + prl_snow

      !----------------------------------------------------------------
      ! Initilize new snow nodes for snowfall / sleet
      !----------------------------------------------------------------

      snl_bef = snl

      CALL newsnow (patchtype,maxsnl,deltim,t_grnd,pg_rain,pg_snow,bifall,&
                    t_precip,zi_soisno(:0),z_soisno(:0),dz_soisno(:0),t_soisno(:0),&
                    wliq_soisno(:0),wice_soisno(:0),fiold(:0),snl,sag,scv,snowdp,fsno)

      ! new snow layer
      IF (snl .lt. snl_bef) THEN
         sabg_lyr(snl+1:snl-snl_bef+1) = sabg_lyr(snl_bef+1:1)
         sabg_lyr(snl-snl_bef+2:1) = 0.
      ENDIF

      !----------------------------------------------------------------
      ! Energy and Water balance 
      !----------------------------------------------------------------
      lb  = snl + 1            !lower bound of array 
      lbsn = min(lb,0)

      CALL GLACIER_TEMP (patchtype,   lb    ,nl_soil     ,deltim     ,&
                   zlnd        ,zsno        ,capr       ,cnfac       ,&
                   forc_hgt_u  ,forc_hgt_t  ,forc_hgt_q ,forc_us     ,&
                   forc_vs     ,forc_t      ,forc_q     ,forc_rhoair ,&
                   forc_psrf   ,coszen      ,sabg       ,forc_frl    ,&
                   fsno,dz_soisno(lb:),z_soisno(lb:),zi_soisno(lb-1:),&
                   t_soisno(lb:),wice_soisno(lb:),wliq_soisno(lb:)   ,&
                   scv         ,snowdp      ,imelt(lb:) ,taux        ,&
                   tauy        ,fsena       ,fevpa      ,lfevpa      ,&
                   fseng       ,fevpg       ,olrg       ,fgrnd       ,&
                   qseva       ,qsdew       ,qsubl      ,qfros       ,&
                   sm          ,tref        ,qref       ,trad        ,&
                   errore      ,emis        ,z0m        ,zol         ,&
                   rib         ,ustar       ,qstar      ,tstar       ,&
                   fm          ,fh          ,fq         ,pg_rain     ,&
                   pg_snow     ,t_precip    ,                         &
                   snofrz(lbsn:0), sabg_lyr(lb:1)               )


#ifdef SNICAR
      CALL GLACIER_WATER_snicar (nl_soil    ,maxsnl     ,deltim      ,&
                   z_soisno    ,dz_soisno   ,zi_soisno  ,t_soisno    ,&
                   wliq_soisno ,wice_soisno ,pg_rain    ,pg_snow     ,&
                   sm          ,scv         ,snowdp     ,imelt       ,&
                   fiold       ,snl         ,qseva      ,qsdew       ,&
                   qsubl       ,qfros       ,rsur       ,rnof        ,&
                   ssi         ,wimp                                 ,&
                   ! SNICAR
                   forc_aer    ,&
                   mss_bcpho   ,mss_bcphi   ,mss_ocpho  ,mss_ocphi   ,&
                   mss_dst1    ,mss_dst2    ,mss_dst3   ,mss_dst4     )
#else
      CALL GLACIER_WATER (nl_soil,maxsnl,deltim                      ,&
                   z_soisno    ,dz_soisno   ,zi_soisno  ,t_soisno    ,&
                   wliq_soisno ,wice_soisno ,pg_rain    ,pg_snow     ,&
                   sm          ,scv         ,snowdp     ,imelt       ,&
                   fiold       ,snl         ,qseva      ,qsdew       ,&
                   qsubl       ,qfros       ,rsur       ,rnof        ,&
                   ssi         ,wimp                                  )
#endif


      lb = snl + 1
      t_grnd = t_soisno(lb)

      ! ----------------------------------------
      ! energy and water balance check
      ! ----------------------------------------
      zerr=errore

      endwb=scv+sum(wice_soisno(1:)+wliq_soisno(1:))
      errorw=(endwb-totwb)-(pg_rain+pg_snow-fevpa-rnof)*deltim
      xerr=errorw/deltim

!======================================================================

ELSE IF(patchtype == 4) THEN   ! <=== is LAND WATER BODIES (lake, reservior and river) (patchtype = 4) 

!======================================================================

      snl = 0
      DO j = maxsnl+1, 0
         IF (wliq_soisno(j)+wice_soisno(j) > 0.) THEN
            snl=snl-1
         ENDIF
      ENDDO

      zi_soisno(0) = 0.
      IF (snl < 0) THEN
         DO j = -1, snl, -1
            zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
         ENDDO
      ENDIF

      DO j = 1,nl_soil
         zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
      ENDDO

      scvold = scv          !snow mass at previous time step
      fiold(:) = 0.0
      IF (snl < 0) THEN
         fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
      ENDIF

      w_old = sum(wliq_soisno(1:)) + sum(wice_soisno(1:))

      pg_rain = prc_rain + prl_rain
      pg_snow = prc_snow + prl_snow

      CALL newsnow_lake ( &
           ! "in" arguments
           ! ---------------
           maxsnl       ,nl_lake      ,deltim          ,dz_lake         ,&
           pg_rain      ,pg_snow      ,t_precip        ,bifall          ,&

           ! "inout" arguments
           ! ------------------
           t_lake       ,zi_soisno(:0),z_soisno(:0)    ,&
           dz_soisno(:0),t_soisno(:0) ,wliq_soisno(:0) ,wice_soisno(:0) ,&
           fiold(:0)    ,snl          ,sag             ,scv             ,&
           snowdp       ,lake_icefrac )

      CALL laketem ( &
           ! "in" laketem arguments
           ! ---------------------------
           patchtype    ,maxsnl       ,nl_soil         ,nl_lake         ,&
           patchlatr    ,deltim       ,forc_hgt_u      ,forc_hgt_t      ,&
           forc_hgt_q   ,forc_us      ,forc_vs         ,forc_t          ,&
           forc_q       ,forc_rhoair  ,forc_psrf       ,forc_sols       ,&
           forc_soll    ,forc_solsd   ,forc_solld      ,sabg            ,&
           forc_frl     ,dz_soisno    ,z_soisno        ,zi_soisno       ,&
           dz_lake      ,lakedepth    ,vf_quartz       ,vf_gravels      ,&
           vf_om        ,vf_sand      ,wf_gravels      ,wf_sand         ,&
           porsl        ,csol         ,k_solids        , &
           dksatu       ,dksatf       ,dkdry           , &
#ifdef THERMAL_CONDUCTIVITY_SCHEME_4
           BA_alpha     ,BA_beta, &
#endif

           ! "inout" laketem arguments
           ! ---------------------------
           t_grnd       ,scv          ,snowdp          ,t_soisno        ,&
           wliq_soisno  ,wice_soisno  ,imelt           ,t_lake          ,&
           lake_icefrac ,savedtke1, &

           ! SNICAR
           snofrz       ,sabg_lyr     ,&

           ! "out" laketem arguments
           ! ---------------------------
           taux         ,tauy         ,fsena                            ,&
           fevpa        ,lfevpa       ,fseng           ,fevpg           ,&
           qseva        ,qsubl        ,qsdew           ,qfros           ,&
           olrg         ,fgrnd        ,tref            ,qref            ,&
           trad         ,emis         ,z0m             ,zol             ,&
           rib          ,ustar        ,qstar           ,tstar           ,&
           fm           ,fh           ,fq              ,sm )

      CALL snowwater_lake ( &
           ! "in" snowater_lake arguments
           ! ---------------------------
           maxsnl       ,nl_soil      ,nl_lake         ,deltim          ,&
           ssi          ,wimp         ,porsl           ,pg_rain         ,&
           pg_snow      ,dz_lake      ,imelt(:0)       ,fiold(:0)       ,&
           qseva        ,qsubl        ,qsdew           ,qfros           ,&

           ! "inout" snowater_lake arguments
           ! ---------------------------
           z_soisno     ,dz_soisno    ,zi_soisno       ,t_soisno        ,&
           wice_soisno  ,wliq_soisno  ,t_lake          ,lake_icefrac    ,&
           fseng        ,fgrnd        ,snl             ,scv             ,&
           snowdp       ,sm            & 

#ifdef SNICAR
           ! SNICAR
           ,forc_aer    ,&
           mss_bcpho    ,mss_bcphi    ,mss_ocpho       ,mss_ocphi       ,&
           mss_dst1     ,mss_dst2     ,mss_dst3        ,mss_dst4         &
#endif
           )

      ! We assume the land water bodies have zero extra liquid water capacity 
      ! (i.e.,constant capacity), all excess liquid water are put into the runoff,
      ! this unreasonable assumption should be updated in the future version
      a = (sum(wliq_soisno(1:))+sum(wice_soisno(1:))+scv-w_old-scvold)/deltim
      aa = qseva+qsubl-qsdew-qfros
      rsur = max(0., pg_rain + pg_snow - aa - a)
      rnof = rsur

      ! Set zero to the empty node
      IF (snl > maxsnl) THEN
         wice_soisno(maxsnl+1:snl) = 0.
         wliq_soisno(maxsnl+1:snl) = 0.
         t_soisno   (maxsnl+1:snl) = 0.
         z_soisno   (maxsnl+1:snl) = 0.
         dz_soisno  (maxsnl+1:snl) = 0.
      ENDIF

!======================================================================

ELSE                     ! <=== is OCEAN (patchtype >= 99) 

!======================================================================
! simple ocean-sea ice model

    tssea = t_grnd
    tssub (1:7) = t_soisno (1:7) 
    CALL SOCEAN (dosst,deltim,oro,forc_hgt_u,forc_hgt_t,forc_hgt_q,&
                 forc_us,forc_vs,forc_t,forc_t,forc_rhoair,forc_psrf,&
                 sabg,forc_frl,tssea,tssub(1:7),scv,&
                 taux,tauy,fsena,fevpa,lfevpa,fseng,fevpg,tref,qref,&
                 z0m,zol,rib,ustar,qstar,tstar,fm,fh,fq,emis,olrg)
                 
               ! null data for sea component
                 z_soisno   (:) = 0.0
                 dz_soisno  (:) = 0.0
                 t_soisno   (:) = 0.0
                 t_soisno (1:7) = tssub(1:7)
                 wliq_soisno(:) = 0.0
                 wice_soisno(:) = 0.0
                 t_grnd  = tssea
                 snowdp  = scv/1000.*20. 

                 trad    = tssea
                 fgrnd   = 0.0
                 rsur    = 0.0
                 rnof    = 0.0

!======================================================================

ENDIF
#if(defined CaMa_Flood)
if (LWINFILT) then 
   if (qinfl_fld<0.0) fevpg_fld=0.0d0
   flddepth        =  flddepth- qinfl_fld*deltim !mm
endif
if (LWEVAP) then 
   if ((flddepth .GT. 1.e-6).and.(fldfrc .GT. 0.05).and.patchtype == 0)then
         call fldfluxes (forc_hgt_u,forc_hgt_t,forc_hgt_q,&
            forc_us,forc_vs,forc_t,forc_q,forc_rhoair,forc_psrf,t_grnd,&
            taux_fld,tauy_fld,fseng_fld,fevpg_fld,tref_fld,qref_fld,&
            z0m_fld,zol_fld,rib_fld,ustar_fld,qstar_fld,tstar_fld,fm_fld,fh_fld,fq_fld)
      if (fevpg_fld<0.0) fevpg_fld=0.0d0
      if ((flddepth-deltim*fevpg_fld .GT. 0.0) .and. (fevpg_fld.GT.0.0)) then
         flddepth=flddepth-deltim*fevpg_fld
         !taux= taux_fld*fldfrc+(1.0-fldfrc)*taux
         !tauy= tauy_fld*fldfrc+(1.0-fldfrc)*tauy
         fseng= fseng_fld*fldfrc+(1.0-fldfrc)*fseng
         fevpg= fevpg_fld*fldfrc+(1.0-fldfrc)*fevpg
         !tref=tref_fld*fldfrc+(1.0-fldfrc)*tref! 2 m height air temperature [kelvin]
         !qref=qref_fld*fldfrc+(1.0-fldfrc)*qref! 2 m height air humidity
         !z0m=z0m_fld*fldfrc+(1.0-fldfrc)*z0m! effective roughness [m]
         !zol=zol_fld*fldfrc+(1.0-fldfrc)*zol! dimensionless height (z/L) used in Monin-Obukhov theory
         !rib=rib_fld*fldfrc+(1.0-fldfrc)*rib! bulk Richardson number in surface layer
         !ustar=ustar_fld*fldfrc+(1.0-fldfrc)*ustar! friction velocity [m/s]
         !tstar=tstar_fld*fldfrc+(1.0-fldfrc)*tstar! temperature scaling parameter
         !qstar=qstar_fld*fldfrc+(1.0-fldfrc)*qstar! moisture scaling parameter
         !fm=fm_fld*fldfrc+(1.0-fldfrc)*fm! integral of profile function for momentum
         !fh=fh_fld*fldfrc+(1.0-fldfrc)*fh! integral of profile function for heat
         !fq=fq_fld*fldfrc+(1.0-fldfrc)*fq!,       &! integral of profile function for moisture
      else
         fevpg_fld=0.0d0
      endif
   else
      fevpg_fld=0.0d0
   endif 
else
   fevpg_fld=0.0d0
endif

#endif


!======================================================================
! Preparation for the next time step
! 1) time-varying parameters for vegatation
! 2) fraction of snow cover 
! 3) solar zenith angle and
! 4) albedos 
!======================================================================

    ! cosine of solar zenith angle 
    calday = calendarday(idate)
    coszen = orb_coszen(calday,patchlonr,patchlatr)

    IF (patchtype <= 5) THEN   !LAND
#if(defined DYN_PHENOLOGY)
       ! need to update lai and sai, fveg, green, they are done once in a day only
       IF (dolai) THEN
          CALL LAI_empirical(patchclass,nl_soil,rootfr,t_soisno(1:),lai,sai,fveg,green)
       ENDIF
#endif

! ONLY for soil patches
!NOTE: lai from remote sensing has already considered snow coverage
IF (patchtype == 0) THEN

#if(defined USGS_CLASSIFICATION || defined IGBP_CLASSIFICATION)
       CALL snowfraction (tlai(ipatch),tsai(ipatch),z0m,zlnd,scv,snowdp,wt,sigf,fsno)
       lai = tlai(ipatch)
       sai = tsai(ipatch) * sigf
#endif

#ifdef PFT_CLASSIFICATION
       ps = patch_pft_s(ipatch)      
       pe = patch_pft_e(ipatch)
       CALL snowfraction_pftwrap (ipatch,zlnd,scv,snowdp,wt,sigf,fsno)
       lai_p(ps:pe) = tlai_p(ps:pe)
       sai_p(ps:pe) = tsai_p(ps:pe) * sigf_p(ps:pe)
       lai = tlai(ipatch)
       sai = sum(sai_p(ps:pe)*pftfrac(ps:pe))
#endif

#ifdef PC_CLASSIFICATION
       pc = patch2pc(ipatch)
       CALL snowfraction_pcwrap (ipatch,zlnd,scv,snowdp,wt,sigf,fsno)
       lai_c(:,pc) = tlai_c(:,pc)
       sai_c(:,pc) = tsai_c(:,pc) * sigf_c(:,pc)
       lai = tlai(ipatch)
       sai = sum(sai_c(:,pc)*pcfrac(:,pc))
#endif

ELSE
       CALL snowfraction (tlai(ipatch),tsai(ipatch),z0m,zlnd,scv,snowdp,wt,sigf,fsno)
       lai = tlai(ipatch)
       sai = tsai(ipatch) * sigf
ENDIF

       ! update the snow age 
       IF (snl == 0) sag=0.
       CALL snowage (deltim,t_grnd,scv,scvold,sag)

       ! water volumetric content of soil surface layer [m3/m3]
       ssw = min(1.,1.e-3*wliq_soisno(1)/dz_soisno(1))
       IF (patchtype >= 3) ssw = 1.0

! ============================================================================
!  Calculate column-integrated aerosol masses, and
!  mass concentrations for radiative calculations and output
!  (based on new snow level state, after SnowFilter is rebuilt.
!  NEEDS TO BE AFTER SnowFiler is rebuilt, otherwise there
!  can be zero snow layers but an active column in filter)

       CALL AerosolMasses( snl ,do_capsnow ,&
            wice_soisno(:0),wliq_soisno(:0),snwcp_ice      ,snw_rds       ,&

            mss_bcpho     ,mss_bcphi       ,mss_ocpho      ,mss_ocphi     ,&
            mss_dst1      ,mss_dst2        ,mss_dst3       ,mss_dst4      ,&

            mss_cnc_bcphi ,mss_cnc_bcpho   ,mss_cnc_ocphi  ,mss_cnc_ocpho ,&
            mss_cnc_dst1  ,mss_cnc_dst2    ,mss_cnc_dst3   ,mss_cnc_dst4  )

! ============================================================================
! Snow aging routine based on Flanner and Zender (2006), Linking snowpack
! microphysics and albedo evolution, JGR, and Brun (1989), Investigation of
! wet-snow metamorphism in respect of liquid-water content, Ann. Glaciol.

       dz_soisno_(:1) = dz_soisno(:1)
       t_soisno_ (:1) = t_soisno (:1)

       IF (patchtype == 4) THEN
          dz_soisno_(1) = dz_lake(1)
          t_soisno_ (1) = t_lake (1)
       ENDIF

       CALL SnowAge_grain(   deltim ,snl    ,dz_soisno_(:1) ,&
            pg_snow         ,snwcp_ice      ,snofrz         ,&

            do_capsnow      ,fsno           ,scv            ,&
            wliq_soisno (:0),wice_soisno(:0),&
            t_soisno_   (:1),t_grnd         ,&
            snw_rds         )

! ============================================================================
       ! albedos 
       ! we supposed CALL it every time-step, because 
       ! other vegeation related parameters are needed to create
       IF (doalb) THEN
            CALL albland (ipatch, patchtype,&
                 soil_s_v_alb,soil_d_v_alb,soil_s_n_alb,soil_d_n_alb,&
                 chil,rho,tau,fveg,green,lai,sai,coszen,wt,fsno,scv,sag,ssw,t_grnd,&
                 snl,wliq_soisno,wice_soisno,snw_rds,&
                 mss_cnc_bcpho,mss_cnc_bcphi,mss_cnc_ocpho,mss_cnc_ocphi,&
                 mss_cnc_dst1,mss_cnc_dst2,mss_cnc_dst3,mss_cnc_dst4,&
                 alb,ssun,ssha,ssno,thermk,extkb,extkd)
       ENDIF
    ELSE                   !OCEAN
       sag = 0.0
       IF(doalb)THEN
            CALL albocean (oro,scv,coszen,alb)
       ENDIF
    ENDIF

    ! zero-filling set for glacier/ice-sheet/land water bodies/ocean components
    IF (patchtype > 2) THEN
       lai = 0.0
       sai = 0.0
       laisun = 0.0
       laisha = 0.0
       green = 0.0
       fveg = 0.0
       sigf = 0.0

       ssun(:,:) = 0.0
       ssha(:,:) = 0.0
       thermk = 0.0
       extkb = 0.0
       extkd = 0.0

      tleaf = forc_t
      ldew_rain  = 0.0
      ldew_snow  = 0.0
      ldew  = 0.0
      fsenl = 0.0
      fevpl = 0.0
      etr   = 0.0
      assim = 0.0
      respc = 0.0

      zerr=0.
      xerr=0.

       qinfl = 0.
       qdrip = forc_rain + forc_snow
       qintr = 0.
       h2osoi = 0. 
       rstfacsun_out = 0.
       rstfacsha_out = 0.
       gssun_out = 0.
       gssha_out = 0.
#ifdef WUEdiag
       assimsun_out           =0.
       etrsun_out             =0.
       assim_RuBP_sun_out     =0.
       assim_Rubisco_sun_out  =0.
       cisun_out              =0.
       Dsun_out               =0.
       gammasun_out           =0.
       lambdasun_out          =0.
       assimsha_out           =0.
       etrsha_out             =0.
       assim_RuBP_sha_out     =0.
       assim_Rubisco_sha_out  =0.
       cisha_out              =0.
       Dsha_out               =0.
       gammasha_out           =0.
       lambdasha_out          =0.
       lambda_out             =0.
#endif
       rootr = 0.
       zwt = 0.
      
       IF (DEF_USE_VARIABLY_SATURATED_FLOW) THEN
          wa = 0.
       ELSE
          wa = 4800.
       ENDIF

       qcharge = 0.
#ifdef PLANT_HYDRAULIC_STRESS
       vegwp = -2.5e4
#endif
    ENDIF
      
    h2osoi = wliq_soisno(1:)/(dz_soisno(1:)*denh2o) + wice_soisno(1:)/(dz_soisno(1:)*denice)
      
    IF (DEF_USE_VARIABLY_SATURATED_FLOW) THEN
       wat = sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv
    ELSE
       wat = sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv + wa
    ENDIF
!----------------------------------------------------------------------

END SUBROUTINE CLMMAIN
